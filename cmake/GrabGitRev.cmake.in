
# ================================================================================
# Implementation file for the GetGitRevision module.
# ================================================================================

file(READ  "@HEAD_FILE@"  HEAD_CONTENTS LIMIT 1024)
string(STRIP "${HEAD_CONTENTS}" HEAD_CONTENTS)

if(HEAD_CONTENTS MATCHES "ref:")
    # at a named brach
    string(REPLACE "ref: " "" HEAD_REF "${HEAD_CONTENTS}")
    string(STRIP "${HEAD_REF}" HEAD_REF)
    if(EXISTS "@GIT_DIR@/${HEAD_REF}")
        # found the ref!
        configure_file("@GIT_DIR@/${HEAD_REF}" "@GIT_DATA_DIR@/head-ref" COPYONLY)
        file(READ "@GIT_DATA_DIR@/head-ref" HEAD_HASH LIMIT 1024)
    else()
        # need to locate a packed ref
        configure_file("@GIT_DIR@/packed-refs" "@GIT_DATA_DIR@/packed-refs" COPYONLY)
        file(READ "@GIT_DATA_DIR@/packed-refs" PACKED_REFS_CONTENTS)
        if(${PACKED_REFS_CONTENTS} MATCHES "([a-z0-9]*) ${HEAD_REF}")
            set(HEAD_HASH "${CMAKE_MATCH_1}")
        endif()
    endif()
else()
    # at a detached head
    configure_file("${GIT_DIR}/HEAD" "${GIT_DATA_DIR}/head-ref" COPYONLY) 
    file(READ "@GIT_DATA_DIR@/head-ref" HEAD_HASH LIMIT 1024)
endif()

